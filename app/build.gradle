apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "ru.zakoulov.weatherapp"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    signingConfigs {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("keystore.properties").newDataInputStream())

        release {
            keyAlias properties.getProperty("keyAlias", "")
            keyPassword properties.getProperty("keyPassword", "")
            storeFile file(properties.getProperty("storeFile", ""))
            storePassword properties.getProperty("storePassword", "")
        }
    }

    buildTypes {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        def apiAccuweatherKey = properties.getProperty("api.accuweather.key", "")

        debug {
            applicationIdSuffix ".dev"

            resValue 'string', 'api_accuweather_key', apiAccuweatherKey
        }

        release {
            signingConfig signingConfigs.release
            resValue 'string', 'api_accuweather_key', apiAccuweatherKey
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation 'com.google.android.material:material:1.1.0'

    // Retrofit2
    implementation "com.squareup.retrofit2:retrofit:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2GsonConverterVersion"

    // Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    // Okhttp3
    implementation "com.squareup.okhttp3:okhttp:$okhttp3Version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3Version"
}
